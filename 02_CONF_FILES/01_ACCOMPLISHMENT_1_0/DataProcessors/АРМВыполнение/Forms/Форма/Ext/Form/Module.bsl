
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуВыполнение();		

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеСпецификаций(Команда)
	
	ЗаполнитьТаблицуВыполнение();		
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВыполнение()
	
	Выполнение.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпецификацииКлиентовТовары.Ссылка КАК Спецификация,
		|	СпецификацииКлиентовТовары.Ссылка.Клиент КАК Клиент,
		|	СпецификацииКлиентовТовары.Ссылка.Наименование КАК СпецификацияПредставление,
		|	СпецификацииКлиентовТовары.НомерСтроки КАК НомерПоСпецификации,
		|	СпецификацииКлиентовТовары.НоменклатураКлиента КАК НоменклатураКлиента,
		|	СпецификацииКлиентовТовары.Количество КАК КоличествоПоСпецификации,
		|	СпецификацииКлиентовТовары.Цена КАК Цена,
		|	СпецификацииКлиентовТовары.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_ДанныеСпецификаций
		|ИЗ
		|	Справочник.СпецификацииКлиентов.Товары КАК СпецификацииКлиентовТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровПоСпецификациям.Спецификация КАК Спецификация,
		|	ПоступлениеТоваровПоСпецификациям.НомерПоСпецификации КАК НомерПоСпецификации,
		|	ПоступлениеТоваровПоСпецификациям.НоменклатураКлиента КАК НоменклатураКлиента,
		|	ПоступлениеТоваровПоСпецификациям.Период КАК Период,
		|	ПоступлениеТоваровПоСпецификациям.Регистратор КАК Регистратор,
		|	ПоступлениеТоваровПоСпецификациям.Цена КАК Цена,
		|	ПоступлениеТоваровПоСпецификациям.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровПоСпецификациям.Поставщик КАК Поставщик,
		|	СУММА(ПоступлениеТоваровПоСпецификациям.Количество) КАК КоличествоПоступило
		|ПОМЕСТИТЬ ВТ_ДанныеПоступлений
		|ИЗ
		|	РегистрНакопления.ПоступлениеТоваровПоСпецификациям КАК ПоступлениеТоваровПоСпецификациям
		|ГДЕ
		|	ПоступлениеТоваровПоСпецификациям.Активность
		|	И ПоступлениеТоваровПоСпецификациям.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровПоСпецификациям.Спецификация,
		|	ПоступлениеТоваровПоСпецификациям.НомерПоСпецификации,
		|	ПоступлениеТоваровПоСпецификациям.НоменклатураКлиента,
		|	ПоступлениеТоваровПоСпецификациям.Период,
		|	ПоступлениеТоваровПоСпецификациям.Регистратор,
		|	ПоступлениеТоваровПоСпецификациям.Цена,
		|	ПоступлениеТоваровПоСпецификациям.Номенклатура,
		|	ПоступлениеТоваровПоСпецификациям.Поставщик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеСпецификаций.Спецификация КАК Спецификация,
		|	ВТ_ДанныеСпецификаций.Клиент КАК Клиент,
		|	ВТ_ДанныеСпецификаций.СпецификацияПредставление КАК СпецификацияПредставление,
		|	ВТ_ДанныеСпецификаций.НомерПоСпецификации КАК НомерПоСпецификации,
		|	ВТ_ДанныеСпецификаций.НоменклатураКлиента КАК НоменклатураКлиента,
		|	ВТ_ДанныеПоступлений.КоличествоПоступило КАК КоличествоПоСпецификации,
		|	ВТ_ДанныеСпецификаций.Цена КАК Цена,
		|	ВТ_ДанныеСпецификаций.Сумма КАК Сумма,
		|	ВТ_ДанныеПоступлений.КоличествоПоступило КАК КоличествоПоступило,
		|	ВТ_ДанныеПоступлений.Номенклатура КАК Номенклатура,
		|	ВТ_ДанныеПоступлений.Цена КАК ЦенаЗакупки,
		|	ВТ_ДанныеПоступлений.Поставщик КАК Поставщик
		|ПОМЕСТИТЬ ВТ_ПоступленияИОстаткиКЗакупке
		|ИЗ
		|	ВТ_ДанныеПоступлений КАК ВТ_ДанныеПоступлений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеСпецификаций КАК ВТ_ДанныеСпецификаций
		|		ПО (ВТ_ДанныеСпецификаций.Спецификация = ВТ_ДанныеПоступлений.Спецификация)
		|			И (ВТ_ДанныеСпецификаций.НомерПоСпецификации = ВТ_ДанныеПоступлений.НомерПоСпецификации)
		|			И (ВТ_ДанныеСпецификаций.НоменклатураКлиента = ВТ_ДанныеПоступлений.НоменклатураКлиента)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ДанныеСпецификаций.Спецификация,
		|	ВТ_ДанныеСпецификаций.Клиент,
		|	ВТ_ДанныеСпецификаций.СпецификацияПредставление,
		|	ВТ_ДанныеСпецификаций.НомерПоСпецификации,
		|	ВТ_ДанныеСпецификаций.НоменклатураКлиента,
		|	ВТ_ДанныеСпецификаций.КоличествоПоСпецификации - ЕСТЬNULL(ВложенныйЗапрос.КоличествоПоступило, 0),
		|	ВТ_ДанныеСпецификаций.Цена,
		|	ВТ_ДанныеСпецификаций.Сумма,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	ВТ_ДанныеСпецификаций КАК ВТ_ДанныеСпецификаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВТ_ДанныеПоступлений.Спецификация КАК Спецификация,
		|			ВТ_ДанныеПоступлений.НомерПоСпецификации КАК НомерПоСпецификации,
		|			ВТ_ДанныеПоступлений.НоменклатураКлиента КАК НоменклатураКлиента,
		|			СУММА(ВТ_ДанныеПоступлений.КоличествоПоступило) КАК КоличествоПоступило
		|		ИЗ
		|			ВТ_ДанныеПоступлений КАК ВТ_ДанныеПоступлений
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ВТ_ДанныеПоступлений.Спецификация,
		|			ВТ_ДанныеПоступлений.НомерПоСпецификации,
		|			ВТ_ДанныеПоступлений.НоменклатураКлиента) КАК ВложенныйЗапрос
		|		ПО ВТ_ДанныеСпецификаций.Спецификация = ВложенныйЗапрос.Спецификация
		|			И ВТ_ДанныеСпецификаций.НомерПоСпецификации = ВложенныйЗапрос.НомерПоСпецификации
		|			И ВТ_ДанныеСпецификаций.НоменклатураКлиента = ВложенныйЗапрос.НоменклатураКлиента
		|ГДЕ
		|	ВТ_ДанныеСпецификаций.КоличествоПоСпецификации - ЕСТЬNULL(ВложенныйЗапрос.КоличествоПоступило, 0) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПоступленияИОстаткиКЗакупке.Спецификация КАК Спецификация,
		|	ВТ_ПоступленияИОстаткиКЗакупке.Клиент КАК Клиент,
		|	ВТ_ПоступленияИОстаткиКЗакупке.СпецификацияПредставление КАК СпецификацияПредставление,
		|	ВТ_ПоступленияИОстаткиКЗакупке.НомерПоСпецификации КАК НомерПоСпецификации,
		|	ВТ_ПоступленияИОстаткиКЗакупке.НоменклатураКлиента КАК НоменклатураКлиента,
		|	ВТ_ПоступленияИОстаткиКЗакупке.КоличествоПоСпецификации КАК КоличествоПоСпецификации,
		|	ВТ_ПоступленияИОстаткиКЗакупке.Цена КАК Цена,
		|	ВТ_ПоступленияИОстаткиКЗакупке.Сумма КАК Сумма,
		|	ВТ_ПоступленияИОстаткиКЗакупке.КоличествоПоступило КАК КоличествоПоступило,
		|	ВТ_ПоступленияИОстаткиКЗакупке.Номенклатура КАК НоменклатураПоставщика,
		|	ВТ_ПоступленияИОстаткиКЗакупке.ЦенаЗакупки КАК ЦенаЗакупки,
		|	ВТ_ПоступленияИОстаткиКЗакупке.Поставщик КАК Поставщик,
		|	ВТ_ПоступленияИОстаткиКЗакупке.ЦенаЗакупки * ВТ_ПоступленияИОстаткиКЗакупке.КоличествоПоступило КАК СуммаЗакупки
		|ИЗ
		|	ВТ_ПоступленияИОстаткиКЗакупке КАК ВТ_ПоступленияИОстаткиКЗакупке
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ПоступленияИОстаткиКЗакупке.Спецификация.ДатаСпецификации,
		|	ВТ_ПоступленияИОстаткиКЗакупке.Спецификация.Клиент,
		|	ВТ_ПоступленияИОстаткиКЗакупке.Спецификация.НомерСпецификации,
		|	НомерПоСпецификации";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Выполнение.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);  
		
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьПоступлениеТоваров(Команда)
	
	ВыделенныеСтроки = Элементы.Выполнение.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда 
		Возврат;	
	КонецЕсли;
	
	МассивНоменклатуры = Новый Массив;	
	
	Для каждого ИдентификаторСтроки  Из ВыделенныеСтроки Цикл
	    СтрокаТЧ = Выполнение.НайтиПоИдентификатору(ИдентификаторСтроки);
		СтруктураДанных = Новый Структура("Спецификация,НомерПоСпецификации,НоменклатураКлиента,КоличествоПоСпецификации");			
		ЗаполнитьЗначенияСвойств(СтруктураДанных,СтрокаТЧ);
		МассивНоменклатуры.Добавить(СтруктураДанных);
		
	КонецЦикла;
	
	ПараметрыОткрытия = Новый Структура;  
	ПараметрыОткрытия.Вставить("МассивНоменклатуры",МассивНоменклатуры);
		
	ОткрытьФорму("Документ.ПоступлениеТоваровУслугПредварительное.ФормаОбъекта"
					,ПараметрыОткрытия
					,ЭтаФорма,,,
					,Новый ОписаниеОповещения("ОформитьПоступлениеТоваровПослеЗакрытия",ЭтаФорма)
					,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьПоступлениеТоваровПослеЗакрытия(РезультатЗакрытия,ДополнительныеПараметры) Экспорт 

	ЗаполнитьТаблицуВыполнение();	

КонецПроцедуры



